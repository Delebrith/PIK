buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		maven {
			url "http://localhost:8081/nexus/content/repositories/central/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
	}
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'maven'

group = 'edu.pw.eiti'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	maven {
		url "http://localhost:8081/nexus/content/repositories/central/"
	}
}

nexus {
	sign = false
	repositoryUrl = 'http://localhost:8081/nexus/content/repositories/releases/'
	snapshotRepositoryUrl = 'http://localhost:8081/nexus/content/repositories/snapshots/'
}

artifacts {
	archives war
}

uploadArchives {
	//environmental variables need to be created and filled with correct credentials
	def username = findProperty("nexusUsername") 
	def password = findProperty("nexusPassword")
	repositories {
		mavenDeployer {
			repository(url: nexus.repositoryUrl) {
				authentication(userName: username, password: password)
			}
			snapshotRepository(url: nexus.snapshotRepositoryUrl) {
				authentication(userName: username, password: password)
			}
		}
	}
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
}

